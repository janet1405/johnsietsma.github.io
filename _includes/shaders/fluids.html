
<script type="x-shader/x-fragment" id="flowMapFrag">
// http://http.developer.nvidia.com/GPUGems/gpugems_ch38.html
//u = advect(u);
//u = diffuse(u);
//u = addForces(u);
//// Now apply the projection operator to the result.
//p = computePressure(u);
//u = subtractPressureGradient(u, p);


void advect(float2 coords : WPOS,   // grid coordinates
	out float4 xNew : COLOR,  // advected qty
	uniform float timestep,
	uniform float rdx,        // 1 / grid scale
	uniform samplerRECT u,    // input velocity
	uniform samplerRECT x)    // qty to advect
{
	// follow the velocity field "back in time"
	float2 pos = coords - timestep * rdx * f2texRECT(u, coords);
	// interpolate and write to the output fragment
	xNew = f4texRECTbilerp(x, pos);
}

void jacobi(half2 coords : WPOS,   // grid coordinates
	out half4 xNew : COLOR,  // result
	uniform half alpha,
	uniform half rBeta,      // reciprocal beta
	uniform samplerRECT x,   // x vector (Ax = b)
	uniform samplerRECT b)   // b vector (Ax = b)
{
	// left, right, bottom, and top x samples
	half4 xL = h4texRECT(x, coords - half2(1, 0));
	half4 xR = h4texRECT(x, coords + half2(1, 0));
	half4 xB = h4texRECT(x, coords - half2(0, 1));
	half4 xT = h4texRECT(x, coords + half2(0, 1));
	
	// b sample, from center
	half4 bC = h4texRECT(b, coords);
	
	// evaluate Jacobi iteration
	xNew = (xL + xR + xB + xT + alpha * bC) * rBeta;
}

void divergence(half2 coords  : WPOS,   // grid coordinates
	out half4 div : COLOR,  // divergence
	uniform half halfrdx,   // 0.5 / gridscale
	uniform samplerRECT w)  // vector field
{
	half4 wL = h4texRECT(w, coords - half2(1, 0));
	half4 wR = h4texRECT(w, coords + half2(1, 0));
	half4 wB = h4texRECT(w, coords - half2(0, 1));
	half4 wT = h4texRECT(w, coords + half2(0, 1));
	
	div = halfrdx * ((wR.x - wL.x) + (wT.y - wB.y));
}

void gradient(half2 coords : WPOS,   // grid coordinates
	out half4 uNew : COLOR,  // new velocity
	uniform half halfrdx,    // 0.5 / gridscale
	uniform samplerRECT p,   // pressure
	uniform samplerRECT w)   // velocity
{
	half pL = h1texRECT(p, coords - half2(1, 0));
	half pR = h1texRECT(p, coords + half2(1, 0));
	half pB = h1texRECT(p, coords - half2(0, 1));
	half pT = h1texRECT(p, coords + half2(0, 1));
	
	uNew = h4texRECT(w, coords);
	uNew.xy -= halfrdx * half2(pR - pL, pT - pB);
}

void boundary(half2 coords : WPOS,    // grid coordinates
	half2 offset : TEX1,    // boundary offset
	out half4 bv : COLOR,   // output value
	uniform half scale,     // scale parameter
	uniform samplerRECT x)  // state field
{
	bv = scale * h4texRECT(x, coords + offset);
}
</script>